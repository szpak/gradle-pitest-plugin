apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

group = 'info.solidsoft.gradle.pitest'

sourceCompatibility = 1.7

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.3.2'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
    createReleaseCommit = true
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /classpath\ 'info\.solidsoft\.gradle\.pitest:gradle-pitest-plugin:.*'/},
                           replacement: {v, p -> "classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:$v\'"}]
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /id\ 'info\.solidsoft\.pitest'\ version\ '.*'/},
                           replacement: {v, p -> "id 'info.solidsoft.pitest' version '$v'"}]
    }
}

project.version = scmVersion.version

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    funcTest {
        java.srcDir file('src/funcTest/java')
        groovy.srcDir file('src/funcTest/groovy')
        resources.srcDir file('src/funcTest/resources') //Broken in Idea - https://youtrack.jetbrains.com/issue/IDEA-128966
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'   //for Spying in Spock
    testCompile 'junit:junit:4.12'

    funcTestCompile sourceSets.main.output
    funcTestCompile sourceSets.test.output  //to make visible funcTest resources moved to test resources as a workaround for Idea bug

    funcTestCompile configurations.testCompile
    funcTestRuntime configurations.testRuntime
    //dependencies from non default configurations are ignored in Idea - https://youtrack.jetbrains.com/issue/IDEA-121728
    /*funcT*/testCompile('com.netflix.nebula:nebula-test:4.0.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task funcTest(type: Test) {
    testClassesDir = sourceSets.funcTest.output.classesDir
    classpath = sourceSets.funcTest.runtimeClasspath

    reports.html {
        destination = file("${reporting.baseDir}/funcTests")
    }
}
funcTest.shouldRunAfter test
check.shouldRunAfter funcTest

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn test, funcTest
}

nexusStaging {
    packageGroup = "info.solidsoft"
    stagingProfileId = "34a3a5beeaa96"
}

modifyPom {
    project {
        name 'Gradle PIT Plugin'
        description 'Gradle plugin for PIT Mutation Testing'
        url 'http://gradle-pitest-plugin.solidsoft.info/'

        scm {
            connection 'scm:git:git://github.com/szpak/gradle-pitest-plugin.git'
            developerConnection 'scm:git:git@github.com:szpak/gradle-pitest-plugin.git'
            url 'https://github.com/szpak/gradle-pitest-plugin'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'despot'
                    role 'developer'
                }
            }
        }
    }
}

uploadArchives.dependsOn funcTest, check

//Artifacts are not placed directly into JCenter, so for now a 'gradle-plugin' attribute has to be set manually to
//'info.solidsoft.pitest:info.solidsoft.gradle.pitest:gradle-pitest-plugin' via GUI after sync

wrapper {
    gradleVersion "2.12"
}
