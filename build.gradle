apply plugin: 'groovy'
apply plugin: 'info.solidsoft.cdeliveryboy'
apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'maven-publish'

group = 'info.solidsoft.gradle.pitest'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle:cdeliveryboy:0.6.0'
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.2'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
    }
}

cDeliveryBoy {
    tasks {
        promoteRepositoryTask = "releaseRepository"
    }
    nexus {
        autoPromote = true
    }
}

scmVersion {
    hooks {
        pre 'fileUpdate', [file       : 'README.md', pattern: { previousVersion, context -> /classpath\ "info\.solidsoft\.gradle\.pitest:gradle-pitest-plugin:$previousVersion"/ },
                           replacement: { currentVersion, context -> "classpath \"info.solidsoft.gradle.pitest:gradle-pitest-plugin:$currentVersion\"" }]
        pre 'fileUpdate', [file       : 'README.md', pattern: { previousVersion, context -> /id\ "info\.solidsoft\.pitest"\ version\ "$previousVersion"/ },
                           replacement: { currentVersion, context -> "id \"info.solidsoft.pitest\" version \"$currentVersion\"" }]
        pre 'fileUpdate', [file       : 'CHANGELOG.md', pattern: { previousVersion, context -> /^##\ ${context.currentVersion}\ -\ Unreleased$/ },
                           replacement: { currentVersion, context -> "## $currentVersion - ${new Date().format( 'yyyy-MM-dd' )}" }]
    }
}

project.version = scmVersion.version

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    funcTest {
        java.srcDir file('src/funcTest/java')
        groovy.srcDir file('src/funcTest/groovy')
        resources.srcDir file('src/funcTest/resources')
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'
    testCompile 'net.bytebuddy:byte-buddy:1.7.5' //for Spying in Spock
    testCompile 'junit:junit:4.12'

    funcTestCompile sourceSets.main.output  //to make production plugin classes visible in functional tests (it's not in testCompile configuration)
    funcTestCompile configurations.testCompile
    funcTestRuntime configurations.testRuntime
    funcTestCompile('com.netflix.nebula:nebula-test:6.1.2') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
}

task funcTest(type: Test) {
    description = 'Run the functional tests.'
    group = 'Verification'
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath
    jvmArgs '-Xmx1g'
}
funcTest.shouldRunAfter test
check.shouldRunAfter funcTest
check.dependsOn funcTestClasses
uploadArchives.dependsOn funcTest, check

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn test, funcTest
}

animalsniffer {
    sourceSets = [project.sourceSets.main]  //just for production classes - Animal Sniffer fails with: 'Undefined reference: void for Spock tests'
                                            //https://github.com/mojohaus/animal-sniffer/issues/27
}

tasks.withType(ru.vyarus.gradle.plugin.animalsniffer.AnimalSniffer) {
    onlyIf { project.hasProperty('compatibility') }
}

nexusStaging {
    packageGroup = "info.solidsoft"
    stagingProfileId = "34a3a5beeaa96"
}

modifyPom {
    project {
        name 'Gradle PIT Plugin'
        description 'Gradle plugin for PIT Mutation Testing'
        url 'http://gradle-pitest-plugin.solidsoft.info/'

        scm {
            connection 'scm:git:git://github.com/szpak/gradle-pitest-plugin.git'
            developerConnection 'scm:git:git@github.com:szpak/gradle-pitest-plugin.git'
            url 'https://github.com/szpak/gradle-pitest-plugin'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'despot'
                    role 'developer'
                }
            }
        }
    }
}

if (hasProperty('pluginPortal')) {

    apply plugin: 'com.gradle.plugin-publish'

    pluginBundle {
        website = 'https://github.com/szpak/gradle-pitest-plugin'
        vcsUrl = 'https://github.com/szpak/gradle-pitest-plugin'

        plugins {
            plugin {
                id = 'info.solidsoft.pitest'
                displayName = 'gradle-pitest-plugin'
                description = 'Gradle plugin for PIT mutation testing'
                tags = ['test', 'pit', 'mutation-testing']
            }
        }

        mavenCoordinates {
            groupId = project.group
            artifactId = project.name
        }
    }

    publishPlugins {
        onlyIf { ciBuild.isInReleaseMode }
        mustRunAfter releaseRepository
    }
    ciBuild.dependsOn publishPlugins
}

if (hasProperty('changelogSync')) {

    task syncChangelog(type: Exec) {
        doFirst { logger.info("Synchronizing changelog with GitHub for version ${project.version}") }
        commandLine 'chandler', 'push', "release/${project.version}", '--tag-prefix=release/'
    }

    syncChangelog {
        onlyIf { ciBuild.isInReleaseMode }
        mustRunAfter releaseRepository
        if (project.tasks.findByName("publishPlugins")) {
            mustRunAfter publishPlugins
        }
    }
    ciBuild.dependsOn syncChangelog
}
