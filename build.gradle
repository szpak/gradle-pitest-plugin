apply plugin: 'groovy'
apply from: "$rootDir/gradle/cdeliveryboy-release.gradle"

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            //for Plugin Publish plugin
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle:cdeliveryboy:0.8.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    funcTest {
        java.srcDir file('src/funcTest/java')
        groovy.srcDir file('src/funcTest/groovy')
        resources.srcDir file('src/funcTest/resources')
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'
    testCompile 'net.bytebuddy:byte-buddy:1.7.9' //for Spying in Spock
    testCompile 'junit:junit:4.12'

    funcTestCompile sourceSets.main.output  //to make production plugin classes visible in functional tests (it's not in testCompile configuration)
    funcTestCompile configurations.testCompile
    funcTestRuntime configurations.testRuntime
    funcTestCompile('com.netflix.nebula:nebula-test:6.2.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task funcTest(type: Test) {
    description = 'Run the functional tests.'
    group = 'Verification'
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath
    jvmArgs '-Xmx1g'
}
funcTest.shouldRunAfter test
check.shouldRunAfter funcTest
check.dependsOn funcTestClasses     //or more generically: tasks.withType(AbstractCompile)
uploadArchives.dependsOn funcTest, check

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn test, funcTest
}
