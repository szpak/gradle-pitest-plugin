buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        //Local/current version of the plugin should be put on a classpath earlier to override that plugin version
//        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.3-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'pl.droidsonroids.pitest'
    apply plugin: 'com.android.library'

    android {
        buildToolsVersion '26.0.0'
        compileSdkVersion 26
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 26
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    version = '1.0'
    group = 'pitest.sample.multimodule'
}

configure(project(":shared")) {
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile 'org.slf4j:slf4j-nop:1.7.21'
    }
}

configure(project(':itest')) {
    apply plugin: 'pl.droidsonroids.pitest'
    apply plugin: 'com.android.library'

    android {
        buildToolsVersion '26.0.0'
        compileSdkVersion 26
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 26
        }
    }
    dependencies {
        compile project(':shared')
    }

    //Additional configuration to resolve :shared project JAR as mutable code path for PIT
    configurations { mutableCodeBase { transitive false } }
    dependencies { mutableCodeBase project(':shared') }
    pitest {
        timestampedReports = false
        mainSourceSets = [project.android.sourceSets.main, project(':shared').android.sourceSets.main]
        additionalMutableCodePaths = [configurations.mutableCodeBase.singleFile]
//        additionalMutableCodePaths = project(':shared').jar.outputs.files.getFiles()  //Should also work
    }
}
